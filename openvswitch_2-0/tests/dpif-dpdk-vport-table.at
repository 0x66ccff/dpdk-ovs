#
# Wrapper macro for groups of dpif-dpdk-vport-table tests
#
# Accepts two parameters: a test group name ($1) and a list of test names to
# run against the executable ($2).
#
m4_define([TEST_DPIF_DPDK_VPORT_TABLE],
  [AT_BANNER([dpif-dpdk -- vport-table -- $1])
   m4_foreach(
     [testname],
     [$2],
     [AT_SETUP([m4_bpatsubst(testname, [-], [ ])])
      AT_KEYWORDS(dpdk dpif-dpdk dpif-datapath-vport-table)
      AT_SKIP_IF([test "$HAVE_DPDK" = no])
      AT_CHECK([$srcdir/test-dpif-dpdk-vport-table testname], [0], [ignore], [])
      AT_CLEANUP])])

#
# Tests
#
TEST_DPIF_DPDK_VPORT_TABLE([construct], [construct-default])

TEST_DPIF_DPDK_VPORT_TABLE([destroy], [
    destroy-after-close,
    destroy-default])

TEST_DPIF_DPDK_VPORT_TABLE([open], [open-default])

TEST_DPIF_DPDK_VPORT_TABLE([close], [
    close-no-file,
    close-default])

TEST_DPIF_DPDK_VPORT_TABLE([exists], [
    exists-file-error,
    exists-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry add], [
    add-invalid-type,
    add-invalid-lcoreid,
    add-invalid-vportid,
    add-invalid-name,
    add-wraparound,
    add-table-full,
    add-duplicate-phy-entry,
    add-default,
    add-previously-used-entry])

TEST_DPIF_DPDK_VPORT_TABLE([entry get next in-use], [
    get-next-inuse-invalid-vportid,
    get-next-inuse-table-empty,
    get-next-inuse-wraparound,
    get-next-inuse-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry reset], [
    reset-invalid-vportid,
    reset-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry reset lcoreid], [
    reset-lcoreid-invalid-vportid,
    reset-lcoreid-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get lcoreid], [
    get-lcoreid-invalid-vportid,
    get-lcoreid-invalid-lcoreid,
    get-lcoreid-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get first inuse], [
    get-first-inuse-invalid-params,
    get-first-inuse-table-empty,
    get-first-inuse-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get type], [
    get-type-invalid-params,
    get-type-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get name], [
    get-name-invalid-params,
    get-name-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get vportid], [
    get-vportid-invalid-params,
    get-vportid-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry get inuse], [
    get-inuse-invalid-vportid,
    get-inuse-invalid-inuse,
    get-inuse-default])

TEST_DPIF_DPDK_VPORT_TABLE([entry set inuse], [
    set-inuse-invalid-vportid,
    set-inuse-default])
