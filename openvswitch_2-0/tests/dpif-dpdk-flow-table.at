#
# Wrapper macro for groups of dpif-dpdk-flow-table tests
#
# Accepts two parameters: a test group name ($1) and a list of test names to
# run against the executable ($2).
#
m4_define([TEST_DPIF_DPDK_FLOW_TABLE],
  [AT_BANNER([dpif-dpdk -- flow-table -- $1])
  m4_foreach(
    [testname],
    [$2],
    [AT_SETUP([m4_bpatsubst(testname, [-], [ ])])
     AT_KEYWORDS(dpdk dpif-dpdk dpif-datapath-flow-table)
     AT_SKIP_IF([test "$HAVE_DPDK" = no])
     AT_CHECK([$srcdir/test-dpif-dpdk-flow-table testname], [0], [ignore], [])
     AT_CLEANUP])])
#
# Tests
#
TEST_DPIF_DPDK_FLOW_TABLE([construct], [construct-default])

TEST_DPIF_DPDK_FLOW_TABLE([destroy], [destroy-default])

TEST_DPIF_DPDK_FLOW_TABLE([open], [open-default])

TEST_DPIF_DPDK_FLOW_TABLE([entry add], [
    add-default,
    add-key-exists,
    add-table-full,
    add-key-null,
    add-handle-null])

TEST_DPIF_DPDK_FLOW_TABLE([entry del], [
    del-default,
    del-key-not-found,
    del-table-empty,
    del-key-null])

TEST_DPIF_DPDK_FLOW_TABLE([entry find], [
    find-default,
    find-key-not-found,
    find-table-empty,
    find-key-null,
    find-handle-null])

TEST_DPIF_DPDK_FLOW_TABLE([entry next], [
    next-default,
    next-table-empty,
    next-key-null,
    next-handle-null,
    next-index-null])

