#
# Wrapper macro for groups of ovdk-stats tests
#
# Accepts two parameters: a test group name ($1) and a list of test names to
# run against the executable ($2).
#
m4_define([TEST_OVDK_STATS],
  [AT_BANNER([ovdk-stats -- ovdk-stats -- $1])
  m4_foreach(
    [testname],
    [$2],
    [AT_SETUP([m4_bpatsubst(testname, [-], [ ])])
     AT_KEYWORDS(dpdk ovdk-stats)
     AT_SKIP_IF([test "$HAVE_DPDK" = no])
     AT_CHECK([sudo -E $srcdir/test-ovdk-stats -c 1 -n 4 --no-huge -- testname], [0], [ignore], [])
     AT_CLEANUP])])
#
# Tests
#

TEST_OVDK_STATS([stats init], [
    stats-init-vport,
    stats-init-vswitch-data,
    stats-init-vswitch-control,
    stats-port-out-update,
    stats-vport-get])
